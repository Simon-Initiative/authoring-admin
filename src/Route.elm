module Route exposing (Route(..), fromUrl, href, replaceUrl, routeToString)

import Browser.Navigation as Nav
import Data.Guid exposing (Guid, urlParser)
import Data.ResourceId exposing (ResourceId, urlParser)
import Data.Username exposing (Username)
import Html.Styled exposing (Attribute)
import Html.Styled.Attributes as Attr
import Url exposing (Url)
import Url.Parser as Parser exposing ((</>), Parser, oneOf, s, string)



-- ROUTING


type Route
    = Home
    | Root
    | Packages
    | PackageDetails Guid
    | UserSessions
    | Users
    | UserDetails Guid


parser : Parser (Route -> a) a
parser =
    oneOf
        [ Parser.map Home Parser.top
        , Parser.map Packages (s "packages")
        , Parser.map PackageDetails (s "packages" </> Data.Guid.urlParser)
        , Parser.map UserSessions (s "sessions")
        , Parser.map Users (s "users")
        , Parser.map UserDetails (s "users" </> Data.Guid.urlParser)
        ]



-- PUBLIC HELPERS


href : Route -> Attribute msg
href targetRoute =
    Attr.href (routeToString targetRoute)


replaceUrl : Nav.Key -> Route -> Cmd msg
replaceUrl key route =
    Nav.replaceUrl key (routeToString route)


fromUrl : Url -> Maybe Route
fromUrl url =
    -- The RealWorld spec treats the fragment like a path.
    -- This makes it *literally* the path, so we can proceed
    -- with parsing as if it had been a normal path all along.
    { url | path = Maybe.withDefault "" url.fragment, fragment = Nothing }
        |> Parser.parse parser



-- INTERNAL


routeToString : Route -> String
routeToString page =
    let
        pieces =
            case page of
                Home ->
                    []

                Root ->
                    []

                Packages ->
                    [ "packages" ]

                PackageDetails guid ->
                    [ "packages", Data.Guid.toString guid ]

                UserSessions ->
                    [ "sessions" ]

                Users ->
                    [ "users" ]

                UserDetails guid ->
                    [ "users", Data.Guid.toString guid ]
    in
    "#/" ++ String.join "/" pieces
